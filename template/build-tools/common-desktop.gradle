
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // Plugin for creating runnable distributions (Win/Mac/Linux)
        classpath 'com.bladecoder.packr:packr:2.0' // 3rd party prerelease version
    }
}

apply plugin: 'java'
apply plugin: 'application'

def vnId = rootProject.name
mainClassName = "nl.weeaboo.vn.desktop.DesktopLauncher"
applicationName = vnId

test {
    workingDir = vnRootDir
}

run {
    workingDir = vnRootDir
    ignoreExitValue = true
}

eclipse {
    project {
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/template'
    }
}

task assembleNvl(type: Zip) {
    from vnResDir
    archiveName "${vnId}.nvl"
}
assembleDist.dependsOn assembleNvl

tasks.distTar.enabled = false // Don't bother generating a .tar distribution

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
   baseName = applicationName
   classifier = null
   version = null
}

import com.badlogicgames.packr.*

// Pack-all tasks that depends on each platform-specific pack task
task packrAll() {    
}
assembleDist.dependsOn packrAll

def platforms = [
    PackrConfig.Platform.Windows64,
    // PackrConfig.Platform.Windows32, // Should be obsolete
    PackrConfig.Platform.Linux64,
    // PackrConfig.Platform.Linux32, // Should be obsolete
    PackrConfig.Platform.MacOS,
]

platforms.each{ platform ->
    def packTask = tasks.create("packr${platform}") {
        dependsOn shadowJar
        dependsOn assembleNvl
        
        doLast {
            def platformId = platform.toString().toLowerCase(Locale.ROOT)                    
            def jrePath = "${buildToolsDir}/jre/jre8-${platformId}.zip".toString()
            def shadowJar = "${projectDir}/build/libs/${rootProject.name}.jar".toString()
            
            def vmArgs = []
            vmArgs += applicationDefaultJvmArgs            
            if (platform == PackrConfig.Platform.MacOS) {
                vmArgs += ['XstartOnFirstThread'] // Required for LWJGL3
            }

            PackrConfig config = new PackrConfig()
            config.executable = rootProject.name
            config.classpath = [shadowJar]
            config.mainClass = mainClassName
            config.resources = assembleNvl.outputs.files.collect() //[vnResDir]

            config.platform = platform
            config.outDir = new File(buildDir, 'packr/' + platformId)
            config.bundleIdentifier = project.group // MacOS app bundle identifier
            config.iconResource = new File(vnResDir, 'icon.icns') // icon attribute is only used by MacOS platform

            config.jdk = jrePath
            config.minimizeJre = new File(buildToolsDir, 'minimize-jre.json')
            config.vmArgs = vmArgs
            
            new Packr().pack(config);
        }
    }
    packrAll.dependsOn packTask
}
