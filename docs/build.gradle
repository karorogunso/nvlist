buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jruby-gradle:jruby-gradle-plugin:+"
    }
}

apply plugin: 'com.github.jruby-gradle.base'

repositories {
    jcenter()
}

dependencies {
    jrubyExec 'rubygems:bundler:1.5.2'
    jrubyExec 'rubygems:jekyll-feed:0.9.2'
    jrubyExec 'rubygems:jekyll:3.4.5'
    jrubyExec 'rubygems:jekyll-theme-minimal:0.0.4'
}

task clean(type: Delete) {
    delete project.buildDir
}

def jekyllTask(name, args) {
    return tasks.create(name, com.github.jrubygradle.JRubyExec) {
        if (args.startsWith('new')) {
            throw new StopExecutionException("'new' isn't allowed by the Gradle task")
        }
        script 'jekyll'
        workingDir project.projectDir
        scriptArgs args.split()
    }
}

// 'jekyllServe' task for running a local Jekyll server
jekyllTask('jekyllServe', 'serve')

// 'jekyllBuild' task that builds the site into the '_site' folder
jekyllTask('jekyllBuild', 'build').with {
    outputs.dir('_site')
}
